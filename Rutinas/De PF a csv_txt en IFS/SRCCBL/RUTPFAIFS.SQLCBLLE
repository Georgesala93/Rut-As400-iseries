       IDENTIFICATION DIVISION.
       PROGRAM-ID. RUTPFAIFS.
       AUTHOR. JORGE SALAMANCA.
       DATE-WRITTEN. 17/03/2021.
      *----------------------------------------------------------------
      * Aplicacion:                                                   |
      * Funcion   :                                                   |
      * Archivos Entrada/Salida:                                      |
      *----------------------------------------------------------------
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       INPUT-OUTPUT SECTION.
      *----------------------------------------------------------------
      *               Declaracion de Archivos                         |
      *----------------------------------------------------------------
       FILE-CONTROL.
      *----------------------------------------------------------------
      * N/A -----------------------------------------------------------
      *
      *
       DATA DIVISION.
      *
      *----------------------------------------------------------------
      *          Declaraci贸n de Definiciones de Archivo               |
      *----------------------------------------------------------------
       FILE SECTION.
      *----------------------------------------------------------------
      * N/A -----------------------------------------------------------
      *
      *----------------------------------------------------------------
      *            Declaracion de Variables de Trabajo                |
      *----------------------------------------------------------------
       WORKING-STORAGE SECTION.
      *
           EXEC SQL
             INCLUDE SQLCA
           END-EXEC.
      *
       01 W-RUTA-IFS                      PIC  X(200)    VALUE SPACES.
       01 W-ARCHIVO-IFS                   PIC  X(20)     VALUE SPACES.
       01 W-VISTA                         PIC  X(20)     VALUE SPACES.
       01 W-CONSULTA                      PIC  X(2000)   VALUE SPACES.
       01 W-EXECSQL                       PIC  X(2000)   VALUE SPACES.
       01 W-COMANDOCL                     PIC  X(0500)   VALUE SPACES.
       01 W-ESTADO                        PIC  9(01)     VALUE ZEROES.
      *
      *----------------------------------------------------------------
      *                        Data Access
      *----------------------------------------------------------------
      *01  xxxxxxxx-REC.
      *    COPY DDS-REGxxxxxx  OF xxxxxxxx.
      *----------------------------------------------------------------
       LINKAGE SECTION.
      *----------------------------------------------------------------
       01 LK-USER       PIC X(010).
      *
      *----------------------------------------------------------------
       PROCEDURE DIVISION USING LK-USER
                                .
      *----------------------------------------------------------------
       INICIAR-PROGRAMA.
      *----------------------------------------------------------------
           EXEC SQL
             SET OPTION COMMIT = *NONE
           END-EXEC
           PERFORM INICIALIZAR
           PERFORM PROCESAR
           PERFORM FINALIZAR.
      *----------------------------------------------------------------
       FINALIZAR-PROGRAMA.
      *----------------------------------------------------------------
           GOBACK.
      *
      *----------------------------------------------------------------
      * Procedimiento : Inicializar.                                  |
      * Descripci贸n   : En este procedimiento se inicializan las      |
      *                 variables principales y se abren los archivos |
      *                 a ser utilizados.                             |
      *----------------------------------------------------------------
       INICIALIZAR.
      *----------------------------------------------------------------
           INITIALIZE W-COMANDOCL W-CONSULTA W-ESTADO W-RUTA-IFS
                    W-ARCHIVO-IFS W-EXECSQL
           MOVE "/home/"        TO W-RUTA-IFS
           CONTINUE
           .
      *----------------------------------------------------------------
      * Procedimiento : PROCESAR.                                     |
      * Descripci贸n   : Se inicia la logica del PGM                   |
      *----------------------------------------------------------------
       PROCESAR.
      *----------------------------------------------------------------
           PERFORM SOLICITA-INF
           PERFORM CREA-VISTA
           IF ( SQLCODE EQUAL ZEROES )
               PERFORM ARMA-COMANDO
               PERFORM EJECUTA-COMANDO
               PERFORM ELIMINA-VISTA
           END-IF
           CONTINUE
           .
      *----------------------------------------------------------------
       CREA-VISTA.
      *----------------------------------------------------------------
           STRING
               "QTEMP/" LK-USER
               DELIMITED BY SIZE INTO W-VISTA
           END-STRING
           STRING
               "CREATE VIEW " FUNCTION TRIM(W-VISTA)
               " AS (" FUNCTION TRIM(W-CONSULTA)
               " ) "
               DELIMITED BY SIZE INTO W-EXECSQL
           END-STRING

           EXEC SQL
               EXECUTE IMMEDIATE :W-EXECSQL
           END-EXEC
           .
      *----------------------------------------------------------------
       SOLICITA-INF.
      *----------------------------------------------------------------
           DISPLAY "DIGITE NOMBRE DEL ARCHIVO DE SALIDA CON EXTENCION"
           ACCEPT W-ARCHIVO-IFS

           DISPLAY "DIGITE CONSULTA"
           ACCEPT W-CONSULTA
           .
      *----------------------------------------------------------------
       ARMA-COMANDO.
      *----------------------------------------------------------------
           STRING
               FUNCTION TRIM(W-RUTA-IFS)
               FUNCTION TRIM(LK-USER)
               FUNCTION TRIM(W-ARCHIVO-IFS)
               DELIMITED BY SIZE INTO W-RUTA-IFS
           END-STRING

           STRING "CPYTOIMPF FROMFILE("
               FUNCTION TRIM(W-VISTA)") "
               "TOSTMF('" FUNCTION TRIM(W-RUTA-IFS) "') "
               "MBROPT(*REPLACE) "
               "STMFCCSID(*PCASCII) RCDDLM(*CRLF) RMVBLANK(*BOTH) "
               "FLDDLM(';') "
               "DECPNT(*COMMA) "
               "ADDCOLNAM(*SQL)"
               DELIMITED BY SIZE INTO W-COMANDOCL
           END-STRING
           CONTINUE
           .
      *----------------------------------------------------------------
       EJECUTA-COMANDO.
      *----------------------------------------------------------------
           CALL "EXECMD" USING W-COMANDOCL W-ESTADO
           END-CALL
           IF ( W-ESTADO NOT EQUAL ZEROES )
               DISPLAY "ERROR AL GENERAR EL ARCHIVO"
           ELSE
               DISPLAY "EL ARCHIVO SE GENERO EN LA RUTA: " W-RUTA-IFS
           END-IF
           CONTINUE
           .
      *----------------------------------------------------------------
       ELIMINA-VISTA.
      *----------------------------------------------------------------
           MOVE SPACES              TO W-EXECSQL
           STRING
               "DROP VIEW " FUNCTION TRIM(W-VISTA)
               DELIMITED BY SIZE INTO W-EXECSQL
           END-STRING

           EXEC SQL
               EXECUTE IMMEDIATE :W-EXECSQL
           END-EXEC
           .
      *----------------------------------------------------------------
      * Procedimiento : Finalizar.                                    |
      * Descripci贸n   : Se cierran los archivos utilizados.           |
      *----------------------------------------------------------------
       FINALIZAR.
      *----------------------------------------------------------------
           CONTINUE
           .
